\import Algebra.Domain.Euclidean
\import Algebra.Group
\import Algebra.Monoid
\import Algebra.Ring
\import Function.Meta
\import Logic (/=, TruncP, inP, truncP)
\import Logic.Meta
\import Paths.Meta
\import Utils
\import lect10(sigmaEq,isProp)
\import lect11(DecEq,propExt)

-- Тип, являющиеся множествами, ведут себя как множества в теории множеств.
-- Это означает, что многие теоремы из нее можно доказать и в теории типов.
-- Например, мы можем доказать теорему Кантора, которая говорит, что мощность множества X строго меньше, чем мощность множества его подмножеств, то есть множества X -> \Prop.

-- Тот факт, что мощность меньше, означает, что существует инъекция из X в X -> \Prop.
-- \lam x => { x }
-- В качестве такой инъекции мы можем просто взять предикат равенства.
-- (=) : X -> (X -> \Prop)

\truncated \data Card : \Set
  | card \Set

-- card : \Set -> Card

\sfunc leq (c1 c2 : Card) : \Prop
  | card A, card B => ∃ (f : A -> B) (isInj f)

-- Тот факт, что мощность строго меньше, означает, что не существует сюръекции вида
-- f : X -> (X -> \Prop)
-- Это легко доказать так же как и в классическом случае.

-- Некоторые факты невозможно доказать в теории типов, так как мы не предполагаем закон исключенного третьего или аксиому выбора.
-- Например, теорема Кантора-Бернштейна, которая говорит, что если одно множество вкладывается в другое, и наоборот, то между ними есть биекция, не доказуема без исключенного третьего.
-- При необходимости мы можем добавить эти классические принципы.
-- Тогда теория станет еще больше похожа на классическую теорию множеств.

-- 1. Сюръективные функции.

\func isInj {A B : \Set} (f : A -> B) => \Pi {x y : A} -> f x = f y -> x = y

-- Сюръективные функции определяютя очевидным образом.
-- Обратите внимание, что мы используем обрезание, так как хотим, чтобы этот тип был утверждением.
\func isSur {A B : \Type} (f : A -> B) : \Prop =>
  \Pi (b : B) -> ∃ (a : A) (f a = b)
  -- \Pi (b : B) ->        \Sigma (a : A) (f a = b)

-- 2. Биекция = инъекция + сюръекция.

\func isBij {A B : \Set} (f : A -> B)
  => \Sigma (g : B -> A) (\Pi (x : A) -> g (f x) = x) (\Pi (y : B) -> f (g y) = y)

\func isBij->isInj {A B : \Set} (f : A -> B) (p : isBij f) : isInj f
  => \lam {x} {y} q => Utils.sym (p.2 x) *> pmap p.1 q *> p.2 y

\func isBij->isSur {A B : \Set} (f : A -> B) (p : isBij f) : isSur f
  => \lam b => inP (p.1 b, p.3 b)

\func isInj+isSur->isBij {A B : \Set} (f : A -> B) (ip : isInj f) (sp : isSur f) : isBij f
  -- \Sigma (a : A) (f a = b)
  => \let t b => \scase sp b \return \Sigma (a : A) (f a = b) \level \lam p q => ext (ip (p.2 *> Utils.sym q.2)) \with {
                   | inP p => p
                 }
     \in (\lam b => (t b).1, \lam a => ip (t (f a)).2, \lam b => (t b).2)

-- 3. Определение Int.

\data Int'
  | pos' Nat
  | neg' Nat

-- Конструкторы в обычных типах данных не пересекаются.
-- Другими словами, мы всегда можем доказать, что con1 не равен con2.

-- Иногда бывает полезно определять типы данных, в которых это будет не верно.
-- Например, удобно определять целые числа как либо положительное натуральное число, либо отрицательное.
-- При этом, эти два конструктора будут пересекаться, так как положительный ноль совпадает с отрицательным.
-- Чтобы определить такой тип данных, нужно в одном или нескольких конструкторов добавить условия, которые говорят как он будет вычисляться.
-- Синтаксис такой же как и для определения функций при помощи паттерн матчинга.
-- Единственное отличие заключается в том, что не обязательно все случаи должны быть покрыты.
-- Например, мы можем определить как neg вычисляется только на zero, но не на suc.
-- Тогда neg 0 будет вычисляться в pos 0, но neg (suc n) будет уже в нормальной форме как обычный конструктор.
{-
\data Int
  | pos Nat
  | neg Nat \with {
    | 0 => pos 0
  }
-}

-- Когда мы определяем функцию над таким типом данных при помощи паттерн матчинга, система проверяет, что ее значение на эквивалентных конструкторах совпадают.
-- Например, следующее определение не пройдет, так как intEx (pos 0) равно 3, а intEx (neg 0) равно 7.
{-
\func intEx (z : Int) : Nat
  | pos n => 3
  | neg n => 7
-}

\func negative' (x : Int) : Int
  | pos n => neg n
  | neg n => pos n

\func abs (x : Int) : Nat
  | pos n => n
  | neg n => n

-- Паттерны, которые вычисляются, можно пропускать в паттерн матчинге.
-- Например, в следующей функции мы не определили ее значение на neg 0, но система принимает это определение.
\func intEx' (z : Int) : Nat
  | pos n => 3
  | neg (suc n) => 7

-- intEx' (neg 0) => intEx' (pos 0) => 3

-- 4. Фактор-множества.

-- Фактор-множества -- это полезная конструкция над множествами.
-- Если A -- множество с отношением эквивалентности ~ на нем, то мы можем определить новое множество A/~ вместе с функцией in~ : A -> A/~, которая делает равными эквивалентные элементы A.
-- A : \Set
-- ~ : A -> A -> \Prop
-- A/~ : \Set
-- in~ : A -> A/~
-- (in~ a = in~ a') <-> (a ~ a')

-- Чтобы определить функцию над A/~, достаточно задать ее на произвольном элементе a : A так, чтобы эквивалентным элементам соответствовали равные результаты.
-- Мы можем использовать примерно следующий синтаксим, но в нем нет доказательства этого условия.
-- Точный синтаксис приведен ниже.
-- \func f (x : A/~) : B
--   | in~ a => b

-- Пример использвания фактор-множеств -- это рациональные числа, которые определяются как пары целых чисел с точностью до эквивалентности.
-- Rat = (\Sigma Int (x : Int) (Not (x = 0))) / ~
-- Где (n,m,p) ~ (n',m',p') тогда и только тогда, когда n * m' = n' * m.

-- Фактор-множества можно определить при помощи типов данных с условиями.
-- Мы просто добавляем конструктор с параметром типа I и двумя условиями на него.
\truncated \data Quotient (A : \Type) (R : A -> A -> \Type) : \Set
  | in~ A
  | equals~ {a a' : A} (R a a') (i : I) \elim i {
    | left => in~ a
    | right => in~ a'
  }

\func Rat => Quotient (\Sigma Int (d : Int) (d /= 0))
                      (\lam p q => p.1 * q.2 = q.1 * p.2)

\func negRat (x : Rat) : Rat
  | in~ p => in~ (negative p.1, p.2, p.3)
  | equals~ {p} {q} r => equals~ {\Sigma Int (d : Int) (d /= 0)} {\lam p q => p.1 * q.2 = q.1 * p.2} {negative p.1, p.2, p.3} {negative q.1, q.2, q.3}
      (Ring.negative_*-left p.1 q.2 *> pmap negative r *> Utils.sym (Ring.negative_*-left q.1 p.2))

-- equals~ {a} {a'} r : I -> Quotient A R
-- path (equals~ {a} {a'} r) : equals~ {a} {a'} r left = equals~ {a} {a'} right

-- Тогда path (eq a a' r) будет задавать равенство между двумя элементами, указанными в этих условиях.
-- В данном случае мы получаем необходимое равенство между эквивалентными элементами.
\func quotientEq {A : \Type} {R : A -> A -> \Type} (a a' : A) (r : R a a') : in~ a = {Quotient A R} in~ a'
  => path (equals~ r)

-- Чтобы определить функцию над Quotient A R, необходимо задать ее на элементе вида inR a и на конструкторе eq,
-- что соответствует доказательству того, что эквивалентным элементам соответствуют равные значения.
\func quotientEx {A : \Type} {R : A -> A -> \Type} {B : \Set}
                 (f : A -> B) (p : \Pi {a a' : A} -> R a a' -> f a = f a')
                 (x : Quotient A R) : B \elim x
  | in~ a => f a
  | equals~ r i => p r @ i

\func inR-sur {A : \Type} {R : A -> A -> \Type} : isSur (in~ {A} {R})
  => \lam x => \case \elim x \with {
                 | in~ a => inP (a, idp)
               }

\truncated \data RSTClosure {A : \Type} (R : A -> A -> \Type) (a b : A) : \Prop
  | refl (a = b)
  | sym (RSTClosure R b a)
  | trans {c : A} (RSTClosure R a c) (RSTClosure R c b)
  | inc (R a b)

\func code {A : \Type} {R : A -> A -> \Type} (x y : Quotient A R) : \Prop \elim x, y
  | in~ a, in~ a1 => RSTClosure R a a1
  | in~ a, equals~ {a'} {a''} r => propExt (\lam r' => trans r' (inc r)) (\lam r' => trans r' (sym (inc r)))
  | equals~ {a'} {a''} r, in~ a => propExt (trans (sym (inc r))) (trans (inc r))

\func code-refl {A : \Type} {R : A -> A -> \Type} (x : Quotient A R) : code x x \elim x
  | in~ a => refl idp

\func quotientEq-inv' {A : \Type} {R : A -> A -> \Type} (x y : Quotient A R) (p : x = y) : code x y \elim p
  | idp => code-refl x

\func quotientEq-inv {A : \Type} {R : A -> A -> \Type} (a a' : A) (p : in~ a = {Quotient A R} in~ a')
  : RSTClosure R a a'
  => quotientEq-inv' _ _ p
